//////////////////////////////////////////////////////////////////////////
// Files Signed by Rare Certificates
//
// This query identifies rarely used certificates based on the number of
// certificates seen issued by the issuer, then how many distinct files 
// were signed by the leaf certificate based on the file's SHA1.  This is
// then joined with DeviceProcessEvents and DeviceImageLoadEvents to 
// identify instances of these files within the enterprise.
//////////////////////////////////////////////////////////////////////////
let StartDate = ago(7d); // This is how far back to search
let RareCerts = (
    DeviceFileCertificateInfo
    | where Timestamp > StartDate 
    | where IsRootSignerMicrosoft == false and IsTrusted == false // Change or remove this row to include trusted certs
    | summarize CertInstances = dcount(SHA1) by SignerHash, IssuerHash // Determine how many files were signed by each cert
    | join DeviceFileCertificateInfo on IssuerHash, SignerHash // Get all of the certificate data back
    | project-away SignerHash1, IssuerHash1
);
union ( 
    RareCerts
    | join kind=inner (
        DeviceProcessEvents
        | where Timestamp > StartDate
        | distinct DeviceId, DeviceName, SHA1, SHA256, FolderPath, FileName
    ) on SHA1
), ( 
    RareCerts
    | join kind=inner ( 
        DeviceImageLoadEvents
        | where Timestamp > StartDate 
        | distinct DeviceId, DeviceName, SHA1, SHA256, FolderPath, FileName
    ) on SHA1
)
| project-away SHA11
| project-reorder CertInstances, Signer, Issuer, DeviceName, FolderPath, SHA256
| top 10000 by CertInstances asc
