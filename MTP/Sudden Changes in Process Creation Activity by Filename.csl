///////////////////////////////////////////////////////////////////////////////
// Sudden Changes in Process Creation Activity (FileName)
//
// This query analyzes process creation events across all endpoints and looks
// for either significant increases or decreases in activity based on the
// new process FileName based on the number of distinct machines that launched
// a process with that FileName within the TimeBinSize.
///////////////////////////////////////////////////////////////////////////////
let StartDate = ago(7d); // Beginning of the analysis period (currently 7 days ago)
let EndDate = ago(0d); // End of the analysis period (currently now)
let TimeBinSize = 1h; // The time block increment (currently 1 hour, larger enterprises may need 1d for 1 day)
let AnomalyThreshold = 2; // A threshold of what we determine to be an anomaly
let MaxResults = 25; // Limit to the maximum number of results that will be output to the graph
let ProcessData = (
    DeviceProcessEvents
    | where Timestamp between (StartDate .. EndDate)
    | extend TimeBin = bin(Timestamp, TimeBinSize) 
    | project DeviceId, TimeBin, FileName
);
ProcessData
| make-series Machines = dcount(DeviceId) default = 0 on TimeBin from StartDate to EndDate step TimeBinSize by FileName
| extend (anomalies, score, baseline) = series_decompose_anomalies(Machines, AnomalyThreshold) // Identify anomalies within the series
| mv-expand anomalies to typeof(int)
| where anomalies != 0 // Look for incremental anomalies
| mv-expand score to typeof(double)
| top MaxResults by score desc // limit results to the largest MaxResults anomalies
| join kind=rightsemi ProcessData on FileName // Filter initial dataset to only those in this set
| summarize machines = dcount(DeviceId) by FileName, TimeBin // Count distinct machines for chart
| render timechart 
